ext.pluginClassNames = 'org.rundeck.plugins.SapR3NodeStepPlugin'
ext.pluginName = 'SAP Node Step Plugin'
ext.pluginDescription = 'Copy and release pre-defined SAP jobs'

apply plugin: 'java'

ext.pluginVersion = 1.2
project.version = 1.2

configurations{
  //declare custom pluginLibs configuration to include only libs for this plugin
  pluginLibs
  //declare compile to extend from pluginLibs so it inherits the dependencies
  compile{
    extendsFrom pluginLibs
  }
}

repositories {
  mavenCentral()
}

dependencies {
  compile(group:'org.slf4j', name: 'slf4j-api', version: '1.7.25')
  compile(group: 'commons-io', name: 'commons-io', version: '2.4')
  compile(group:'org.rundeck', name: 'rundeck-core', version: '3.0.9-20181127')
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
  into "$buildDir/output/lib"
  from configurations.pluginLibs
}

jar {
  //include contents of output dir
  from "$buildDir/output"
  manifest {
    attributes 'Rundeck-Plugin-Classnames': pluginClassNames
    attributes 'Rundeck-Plugin-Name': pluginName
    attributes 'Rundeck-Plugin-Description': pluginDescription
    def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
    attributes 'Rundeck-Plugin-Libs': "${libList}"
    attributes 'Rundeck-Plugin-Version': pluginVersion
    attributes 'Rundeck-Plugin-Archive': 'true'
    attributes 'Rundeck-Plugin-File-Version': project.version
  }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)
